// apply plugins
apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: "com.jfrog.bintray"

// set Java 8 compatibility
sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

task createSourceJar(type: Jar, dependsOn: classes) {
    classifier "sources"
    from sourceSets.main.allSource
}

task createJavadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

// maven parameters
group = 'com.asamm'
version = API_VERSION

// upload archives to local mavem
uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: mavenLocal().getUrl())
        }
    }
}

// generate publication in Maven format
// https://docs.gradle.org/current/dsl/org.gradle.api.publish.maven.MavenPublication.html
publishing {
    publications {
        pubLibLocusApi(MavenPublication) {

            groupId group
            artifactId 'locus-api'
            //noinspection GroovyAssignabilityCheck
            version API_VERSION

            // define source
            from components.java
            artifact createSourceJar
            artifact createJavadocJar

            // improve generated POM file
            pom.withXml {
                asNode().appendNode(
                        'name', 'Locus API')
                asNode().appendNode(
                        'description', 'Core library for Android "Locus Map" application')
                asNode().appendNode(
                        'url', 'https://github.com/asamm/locus-api')
                asNode().appendNode(
                        'inceptionYear', '2017')

                // add license
                def nodeLicenses = asNode().appendNode('licenses')
                def nodeLicense = nodeLicenses.appendNode('license')
                nodeLicense.appendNode('name', 'GNU Lesser General Public License v3.0')
                nodeLicense.appendNode('url', 'https://api.github.com/licenses/lgpl-3.0')
                nodeLicense.appendNode('distribution', 'repo')

                // add "scm"
                def nodeScm = asNode().appendNode('scm')
                nodeScm.appendNode('connection', 'git://github.com/asamm/locus-api.git')
                nodeScm.appendNode('url', 'git://github.com/asamm/locus-api.git')

                // add "developer info"
                def nodeDevelopers = asNode().appendNode('developers')
                def nodeDeveloper = nodeDevelopers.appendNode('developer')
                nodeDeveloper.appendNode('name', 'Asamm software, s. r. o.')
            }
        }
    }
}

// upload library to BinTray
// https://github.com/bintray/gradle-bintray-plugin
bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['pubLibLocusApi']
    publish = true
    override = true

    pkg {
        repo = 'maven'
        name = 'locus-api'
        licenses = ['LGPL-3.0']
        vcsUrl = 'https://github.com/asamm/locus-api.git'

        // optional Github repository
        githubRepo = 'asamm/locus-api'
        // optional Github readme file
        githubReleaseNotesFile = 'README.md'

        //noinspection GroovyAssignabilityCheck
        version {
            name = API_VERSION
            desc = 'Locus API, version ' + API_VERSION
            released  = new Date()
            vcsTag = API_VERSION
        }
    }
}