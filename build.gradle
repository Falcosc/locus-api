// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        // basic repositories
        google()
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.0.1'
//        classpath 'com.getkeepsafe.dexcount:dexcount-gradle-plugin:0.5.4'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.0'
    }
}

// define global parameters
allprojects {
    repositories {
        // basic repositories
        google()
        jcenter()
        mavenCentral()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

configure([
        project(":locus-api-android-sample")]) {

    apply plugin: 'com.android.application'

    android {
        // setup SDK
        compileSdkVersion Integer.valueOf(PARAM_COMPILE_SDK_VERSION)
        buildToolsVersion PARAM_BUILD_TOOLS_VERSION

        // since API23 is Http Apache library only on demand. It is anyway needed for MultiPartEntity,
        // so here we follow this suggestion
        // http://developer.android.com/about/versions/marshmallow/android-6.0-changes.html#behavior-apache-http-client
        useLibrary 'org.apache.http.legacy'

        defaultConfig {
            minSdkVersion Integer.valueOf(PARAM_MIN_SDK_VERSION)
            targetSdkVersion Integer.valueOf(PARAM_TARGET_SDK_VERSION)
        }

        dexOptions {
            jumboMode true
        }

        packagingOptions {
            exclude 'META-INF/LICENSE.txt'
            exclude 'META-INF/NOTICE.txt'
        }

        compileOptions {
            sourceCompatibility JavaVersion.VERSION_1_7
            targetCompatibility JavaVersion.VERSION_1_7
        }

        // enable lint in project
        lintOptions {
            checkReleaseBuilds true
            abortOnError true
            absolutePaths false
            lintConfig file("../lint-global.xml")
        }

        // signing of versions
        signingConfigs {
            debug {
                storeFile file(SIGN_DEBUG_STORE_FILE)
                storePassword = SIGN_DEBUG_STORE_PASSWORD
                keyAlias = SIGN_DEBUG_KEY_ALIAS
                keyPassword = SIGN_DEBUG_KEY_PASSWORD
            }
            release {
                storeFile file(SIGN_RELEASE_STORE_FILE)
                storePassword = SIGN_RELEASE_STORE_PASSWORD
                keyAlias = SIGN_RELEASE_KEY_ALIAS
                keyPassword = SIGN_RELEASE_KEY_PASSWORD
            }
        }

        // building task
        buildTypes {
            debug {
                signingConfig signingConfigs.debug
            }
            release {
                signingConfig signingConfigs.release
            }
        }
    }

    // iterate over all versions
    android.applicationVariants.all { variant ->

        // define base parameters
        println "Config for: " + build

        // rename result file
        variant.outputs.all {
            // generate base name
            def newName = variant.name.capitalize()
            // remove from 'LocusFreeSamsungDebug', last work 'Debug'
            newName = newName.substring(0, newName.length() - variant.buildType.name.length())
            // create full name like 'LocusFreeSamsung_342_3.5.2_debug.apk'
            def baseFileName = "${newName}_${variant.versionCode}_${variant.versionName}_${variant.buildType.name}"
            // set new output file name
            outputFileName = "${baseFileName}.apk"
        }

        // create also release task for "release builds"
        variant.productFlavors.each { flavor ->
            // do not handle certain flavors at all
            def flavorName = flavor.name.capitalize()
            if (flavor.name == '') {
                return;
            }

            // skip invalid build types (so generate only releases versions)
            if (variant.buildType.name != 'release') {
                return;
            }

            // create task for release
            tasks.create(name: variant.buildType.name + flavorName,
                    dependsOn: variant.assemble) {
                group 'Publish'
                description "Assembles and archives $flavorName and its proguard mapping for the $build build"
            }
        }
    }
}
